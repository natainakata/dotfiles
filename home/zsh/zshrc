#!/usr/bin/env zsh

export ZSHRC_DIR=${${(%):-%N}:A:h}

function source {
  ensure_zcompiled $1
  builtin source $1
}

function ensure_zcompiled {
  local compiled="$1.zwc"
  if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
    echo "Compiling $1"
    zcompile $1
  fi
}
ensure_zcompiled ~/.zshrc

export SHELDON_CONFIG_DIR="$ZSHRC_DIR/sheldon"
sheldon_cache="$SHELDON_CONFIG_DIR/sheldon.zsh"
sheldon_toml="$SHELDON_CONFIG_DIR/plugins.toml"
if [[ ! -r "$sheldon_cache" || "$sheldon_toml" -nt "$sheldon_cache" ]]; then
  sheldon source > $sheldon_cache
fi
source "$sheldon_cache"
unset sheldon_cache sheldon_toml

for profile in ${(z)NIX_PROFILES}; do
  fpath+=($profile/share/zsh/site-functions $profile/share/zsh/$ZSH_VERSION/functions $profile/share/zsh/vendor-completions)
done

autoload -Uz compinit && compinit

source $ZSHRC_DIR/preinit.zsh
source $ZSHRC_DIR/init.zsh
zsh-defer source $ZSHRC_DIR/postinit.zsh
zsh-defer source $ZSHRC_DIR/config/fzf.zsh
zsh-defer source $ZSHRC_DIR/config/alias.zsh


zsh-defer unfunction source
